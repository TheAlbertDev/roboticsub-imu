# Control remoto de un servomotor con control de par

Este es un proyecto basado en dos m칩dulos ESP32. El primero, de ahora en adelante ***master***, monitorizar치 su orientaci칩n mediante una [<abbr title="Inertial Measurement Unit">IMU</abbr>](https://es.wikipedia.org/wiki/Unidad_de_medici%C3%B3n_inercial) y la enviar치 inal치mbricamente a un segundo m칩dulo ESP32, de ahora en adelante ***slave***. El *slave* recibir치 la orientaci칩n del *master* y, en funci칩n de esta, orientar치 un servomotor que emula uno de los motores del EndoWrist. A su vez, el *slave* devolver치 al *master* el par de motor medido emulando el contacto con tejidos. Ese par de motor lo enviaremos por conexi칩n serie a RoboDK junto a los 치ngulos Euler que ya est치bamos enviando en la sesi칩n anterior.

## Objetivos

- Monitorizar la orientaci칩n de un m칩dulo ESP32 llamado *master*.
- Enviar la orientaci칩n de manera inal치mbrica a un segundo m칩dulo ESP32 llamado *slave*.
- Orientar un servomotor en *slave* seg칰n la orientaci칩n del *master*.
- Delvolver al *master* una medici칩n del par aplicado por el servomotor.
- Enviar el par,  junto con la orientaci칩n, a RoboDK mediante conexi칩n serie.

## Procedimiento

> El el presente documento se muestra el c칩digo necesario para realizar la comunicaci칩n inal치mbrica entre dos m칩dulos ESP32. El c칩digo **debe de a침adirse** al c칩digo ya existente de la sesi칩n anterior (medici칩n de 치ngulos Euler, env칤o por terminal serie, etc.). El c칩digo de la sesi칩n anterior se ha obviado de los ejemplos para hacerlos m치s claros.

### Comunicaci칩n inal치mbrica

#### Obtener las direcciones MAC

Para realizar las transmisiones, tanto el *master* como el *slave* necesitan saber la direcci칩n MAC del otro. Esta direcci칩n MAC les identifica en una red de manera inequ칤voca. Para obtener la direcci칩n MAC, utilizaremos el siguiente c칩digo:

```c++
// Incluimos la libreria para una conexion WiFi
#include "WiFi.h"
 
void setup(){
  // Configuramos la comunicacion serie
  Serial.begin(115200);
  // Habilitamos el WiFi en modo estacion
  WiFi.mode(WIFI_MODE_STA);
  // Mostramos por terminal la direccion MAC del dispositivo
  Serial.println(WiFi.macAddress());
}
 
void loop(){
  // Aqui no hacemos nada de momento
}
```

De este modo, abrimos un terminal en Arduino IDE y podremos obtener la direcci칩n MAC. Esto hay que ejecutarlo en ambos m칩dulos. **춰Apuntaros el identificador!** Ser치 imprescindible.

### *Sketch* del *master*

En el *master*, ejecutamos el siguiente c칩digo:

```c++
// Incluimos las librerias para el protocolo ESP_NOW y 
// una conexion WiFi
#include <esp_now.h>
#include <WiFi.h>

// Direccion MAC del slave (reemplazar con el que habeis obtenido)
uint8_t slaveMacAddress[] = {0xAC, 0x67, 0xB2, 0x05, 0x64, 0xC0};

// Esta es la estructura de los datos que enviaremos
// De momento solo enviaremos un numero
typedef struct {
    float exampleNumber;
} TxMessage;
// Creamos una varaible con la estructura recien creada
TxMessage dataToSlave;

// Esta es la estructura de los datos que reciviremos
typedef struct {
    float torque;
} RxMessage;
// Creamos una varaible con la estructura recien creada
RxMessage dataFromSlave;

// Funcion que se ejecutara cada vez que se haya recibido un mensaje
void OnDataRecv(const uint8_t * mac, const uint8_t *incomingData, int len) {
  // Copiamos los datos recibidos a nuestra variable dataFromSlave
  memcpy(&dataFromSlave, incomingData, sizeof(dataFromSlave));
  Serial.print("Torque received:\t");
  Serial.println(dataFromSlave.torque, DEC);
}
 
void setup() {
  // Configuramos la comunicacion serie
  Serial.begin(115200);

  // Habilitamos el WiFi en modo estacion
  WiFi.mode(WIFI_STA);

  // Inicializamos ESP-NOW
  if (esp_now_init() != ESP_OK) {
    Serial.println("Error initializing ESP-NOW");
    return;
  }
  
  // Registramos el slave
  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, slaveMacAddress, 6);
  peerInfo.channel = 0;  
  peerInfo.encrypt = false;
  
  // Anadimos el slave     
  if (esp_now_add_peer(&peerInfo) != ESP_OK){
    Serial.println("Failed to add peer");
    return;
  }
  // Configuramos la funcion a utilizar cada vez que se reciva
  // un mensaje
  esp_now_register_recv_cb(OnDataRecv);
}
 
void loop() {

  // Como ejemplo, enviaremos un numero que iremos incrementando
  esp_err_t result = esp_now_send(slaveMacAddress, (uint8_t *) &dataToSlave, sizeof(dataToSlave));
   
  if (result == ESP_OK) {
    Serial.println("Sent with success");
  }
  else {
    Serial.println("Error sending the data");
  }
  
  // Dejaremos 1 segundo entre envios y incrementamos el numero
  // a enviar
  dataToSlave.exampleNumber = dataToSlave.exampleNumber + 1;
  delay(1000);
}
```

#### *Sketch* del *slave*

Ahora toca cargar el c칩digo al *slave*. **Recordad poner la direcci칩n MAC del *master* en este caso.** Omito comentarios de c칩digo que ya estuvieran en el ejemplo anterior:

```c++
#include <esp_now.h>
#include <WiFi.h>

// Direccion MAC del master (reemplazar con el que habeis obtenido)
uint8_t masterMacAddress[] = {0x7C, 0x9E, 0xBD, 0x61, 0xA3, 0x88};

// Esta es la estructura de los datos que enviaremos
typedef struct {
    float torque;
} TxMessage;
// Creamos una varaible con la estructura recien creada
TxMessage dataToMaster;

// Esta es la estructura de los datos que reciviremos
typedef struct {
    float exampleNumber;
} RxMessage;
// Creamos una varaible con la estructura recien creada
RxMessage dataFromMaster;

void OnDataRecv(const uint8_t * mac, const uint8_t *incomingData, int len) {
  // Copiamos los datos recibidos a nuestra variable dataFromMaster
  memcpy(&dataFromMaster, incomingData, sizeof(dataFromMaster));
  Serial.print("Example number received:\t");
  Serial.println(dataFromMaster.exampleNumber, DEC);
}
 
void setup() {
  Serial.begin(115200);
  WiFi.mode(WIFI_STA);

  if (esp_now_init() != ESP_OK) {
    Serial.println("Error initializing ESP-NOW");
    return;
  }
  
  // Registramos el master
  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, masterMacAddress, 6);
  peerInfo.channel = 0;  
  peerInfo.encrypt = false;
  
  // Anadimos el master     
  if (esp_now_add_peer(&peerInfo) != ESP_OK){
    Serial.println("Failed to add peer");
    return;
  }

  esp_now_register_recv_cb(OnDataRecv);
}
 
void loop() {

  // Como ejemplo, enviaremos un numero que iremos incrementando
  // emulando el torque
  esp_err_t result = esp_now_send(masterMacAddress, (uint8_t *) &dataToMaster, sizeof(dataToMaster));
   
  if (result == ESP_OK) {
    Serial.println("Sent with success");
  }
  else {
    Serial.println("Error sending the data");
  }
  
  // Incrementamos el numero a enviar
  dataToMaster.torque = dataToMaster.torque + 1;
  delay(1000);
}
```

#### *Challenge* #1

Bien. Si hemos seguido las instrucciones al pie de la letra, ahora mismo tenemos dos ESP32 comunic치ndose de manera inal치mbrica. Ahora el reto (y eso quiere decir, que ten칠is que hacerlo vosotros) es editar el c칩digo del *master* para incorporar este c칩digo que ten칤ais de la sesi칩n anterior. Es decir, hacer que el *master* lea los 치ngulos de Euler de la IMU y los env칤e al *slave*. 쯈u칠 deber칠is hacer para ello?

- Incorporar la parte del c칩digo de la IMU (importar librer칤a, inicializar la IMU, obtener los datos y guardarlos en una variable, etc.).
- Incorporar el c칩digo de comunicaci칩n.
- Adaptar el c칩digo de comunicaci칩n (modificar las estructuras `RxMessage` y `TxMessage` (tanto del *master* como del *slave*) para incorporar RPW)
- 춰Comprueba que todo funcione antes de seguir adelante!

> Si te clavas en el reto o te salen errores, pel칠ate un poco pero pregunta de inmediato a los profesores. El reto, por su dificultad, est치 pensado para hacerse en 10~15 min. Si tardas m치s no te dar치 tiempo a hacer la pr치ctica en clase. 游땐

### Control de un servomotor

#### Conexionado del servomotor

Vamos a controlar el [servomotor KY66](http://www.ee.ic.ac.uk/pcheung/teaching/DE1_EE/stores/sg90_datasheet.pdf) con el *slave*. Para ello, conectamos nuestro ESP32 en una *protoboard* y conectamos los pines <kbd>VIN</kbd>, <kbd>D33</kbd> y <kbd>GND</kbd> con los cables de color ![rojo](https://via.placeholder.com/15/ff0000/000000?text=+)`rojo`, ![naranja](https://via.placeholder.com/15/FFA500/000000?text=+)`naranja`,  y ![marr칩n](https://via.placeholder.com/15/744700/000000?text=+)`marr칩n`, respectivamente.

#### Posicionamiento del motor

Para controlar el motor utilizaremos la librer칤a [Servo Library for ESP32 de John K. Bennett](https://github.com/madhephaestus/ESP32Servo). Esta librer칤a genera una salida [<abbr title="Pulse-Width Modulation">PWM</abbr>](https://es.wikipedia.org/wiki/Modulaci%C3%B3n_por_ancho_de_pulsos) para fijar la posici칩n del motor. El motor se puede posicionar de -90췈 a 90췈. O lo que es lo mismo, podemos girarlo 180췈. 

Para instalar la librer칤a, en Arduino IDE, nos vamos a `Sketch > Include Library > Manage Libraries`, escribimos `ESP32Servo` en el campo de b칰squeda e instalamos la librer칤a indicada. Ya la hemos instalado 游땔

Vamos a ver el c칩digo para posicionar el servo:

```c++
// Incluimos la libreria del servo
#include <ESP32Servo.h>

// Declaramos una variable para el servo
Servo servoMotor;

void setup() {
  // Configuramos la comunicacion serie
  Serial.begin(115200);

  // Nos agenciamos unos timers internos para poder operar
  // (쯈ue que es un timer? -> https://bit.ly/2Y9BAwg)
  ESP32PWM::allocateTimer(0);
  ESP32PWM::allocateTimer(1);
  ESP32PWM::allocateTimer(2);
  ESP32PWM::allocateTimer(3);
  servoMotor.setPeriodHertz(50);
  
  // Iniciamos el servo indicandole que utilizaremos el pin 33
  servoMotor.attach(33);
}

void loop() {
  
  // Vamos a hacer un bucle en el que el motor vaya de -90 a 0 a +90
  // en intervalos de 1 segundo (acordaros que esto es lo mismo que
  // hacer 0, 90, 180)
  Serial.println("Servo position: -90췈");
  servoMotor.write(0);
  delay(1000);

  Serial.println("Servo position: 0췈");
  servoMotor.write(90);
  delay(1000);

  Serial.println("Servo position: 90췈");
  servoMotor.write(180);
  delay(1000);
}
```

En este instante debemos de tener nuestro servo operando sin problemas 游봅

#### *Challenge* #2

A침ade el c칩digo de posicionamiento del servo al c칩digo del primer apartado de tal modo que el servo se posicione seg칰n **el *pitch*** que enviemos desde el *master*.

### Lectura del torque

Ahora nos falta leer el [torque o par](https://es.wikipedia.org/wiki/Par_motor) que est치 haciendo el motor. Lo mediremos midiendo ("*춰Gracias, Capitan Obvious!*") la potencia el칠ctrica que consume el motor. Acordemonos de cu치l era la expresi칩n de la potencia: P = I췅V

En nuestro caso, la tensi칩n ser치 siempre la misma, as칤 que lo que haremos ser치 medir la corriente. Lo haremos con una resistencia conectada en serie con el motor. Midiendo la ca칤da de tensi칩n en la resistencia (cuyo valor conocemos), podemos obtener la corriente que pasa por el motor. Puesto que la ca칤do de tensi칩n en motor ser치 **casi** constante (ya no ser치 constante puesto que hemos a침adido una resistencia en serie), la potencia es directamente proporciona a la corriente que medimos y el torque/par tambi칠n.

Hasta aqu칤 ser칤a lo normal, pero si lo prob치ramos ver칤amos que no nos da mucha informaci칩n. 쯇orqu칠? Porque el motor intenta ir a la posici칩n designada a m치xima velocidad (m치xima potencia), por lo que no tendr칤amos modo de diferenciar cuando el motor est치 aplicando un torque m치ximo para ir a una posici칩n o cuando lo est치 aplicando porque se ha encontrado un obst치culo.

A칰n as칤, podemos diferencia ambos casos a침adiendo una variable m치s a la medida: el tiempo. Si el tiempo durante el cual el motor aplica el par m치ximo es corto, supondremos que el motor simplemente se ha posicionado en una posici칩n determinada. En cambio, si  el par es aplicado durante un tiempo relativamente largo, supondremos que el motor no puede llegar a su posici칩n final (o le cuesta) porque se ha encontrado un obst치culo.

Para a침adir la variable tiempo, simplemente integraremos la medida de la corriente. Es decir, iremos sumando la medida de corriente constantemente y utilizaremos este valor como indicador del par. Es importante que de manera peri칩dica vayamos reinici치ndose esta integral, 춰si no, nos ir칤amos a infinito!

#### Conexionado de la resistencia

Conectad la resistencia en serie con el motor. Hacedlo con el terminal que va a <kbd>GND</kbd>. Obtened mediante el c칩digo de colores el valor de la resistencia.

> 쯈ue no sabes qu칠 significa colocar la resistencia en serie? 쯈ue no sabes qu칠 es lo del c칩digo de colores? No me hagas pregunt치rselo al Dr. Miribel 춰o os hago volver a Electr칩nica Aplicada! 游

Ahora, conectad el nodo entre el <kbd>GND</kbd> del motor y el terminal de la resistencia al pin <kbd>D34</kbd>. Apoyaros/utilizad la *protoboard* para ello.

#### *Sketch* para medir corriente

Para medir la corriente utilizamos el siguiente *sketch* (utilizaremos como base el ejemplo del apartado anterior para tener el motor en movimiento):

```c++
#include <ESP32Servo.h>

Servo servoMotor;

// Constante con el valor de la resistencia que utilizamos
// (Valor totalmente inventado para que no lo useis... poned
// el valor de vuestra resistencia)
const float Rshunt = 1000.0;

// Variable que almacenara el torque
float torque = 0;

// Funcion que utilizaremos para obtener la corriente a partir
// del valor del ADC
float getCurrent(void) {

  uint16_t adcValue = analogRead(34);
  
  return ((float)adcValue/4095.0 * 3.3)/Rshunt;
  
}

void setup() {
  
  Serial.begin(115200);
  
  ESP32PWM::allocateTimer(0);
  ESP32PWM::allocateTimer(1);
  ESP32PWM::allocateTimer(2);
  ESP32PWM::allocateTimer(3);
  servoMotor.setPeriodHertz(50);
  
  servoMotor.attach(33);

  // Configuramos el pin D34 como de entrada
  // Lo utilizaremos para leer un valor analogico
  pinMode(34, INPUT);

  // Posicionamos el motor a un de los extremos
  Serial.println("Servo position: -70췈");
  servoMotor.write(20);
  // Le damos un segundo que llegue a su posici칩n
  delay(2000);

  // Leemos la corriente (se supone que en reposo)
  torque = getCurrent();
  Serial.print("Current:\t");
  Serial.println(torque, DEC);

  // Ahora llevamos el motor al otro extremo
  // y enseguida medimos corriente
  Serial.println("Servo position: 70췈");
  servoMotor.write(160);

}

void loop() {

  // Leemos la corriente y lo sacamos por terminal
  torque = getCurrent();
  Serial.print("Current:\t");
  Serial.println(torque, DEC);
  
}
```

Si llev치is la salida del terminal a una hoja de datos (d칤gase Excel), podr칠is observar la siguiente medici칩n:

![image-20211101134533729](./assets/imgs/torque-trans-no-obstacle.png)

Pod칠is ver c칩mo hay un pico inicial en el que el motor quiere llegar r치pido a su posici칩n final y otro peque침o pico final en el que el motor corrige su error estacionario.

Ahora, si ejecutamos otra vez el programa obstaculizando el motor (frenando con la mano, por ejemplo), obtenemos la siguiente respuesta:

![image-20211101135020818](./assets/imgs/torque-trans-obstacle.png)

En este caso, le he fastidiado bastante y no lo dejaba girar en absoluto, ocasionando que el motor intentara moverse, se parase, intentaba moverse otra vez, y as칤 peri칩dicamente. De ah칤 los pulsos que veis en la gr치fica hasta llegar al final, donde se ve como ya no hay obst치culo.

Como os comentaba, mirando simplemente el valor de la corriente no es suficiente para saber si hay un obst치culo o no. El valor pico ronda los 0.5 A (lo m치ximo que puede dar un puerto USB) y ese valor se alcanza tanto en el caso con y sin obst치culo.

Vamos a integrar la corriente para tener una medici칩n que tenga en cuenta el tiempo.

#### Integraci칩n de la corriente

El *sketch* seria el mismo y simplemente modificar칤amos la funci칩n `getCurrent()` para integrar la corriente durante un determinado tiempo:

```c++
#include <ESP32Servo.h>

Servo servoMotor;

const float Rshunt = 1000.0;

float torque = 0;

float getCurrent(uint32_t integrationTimeMs) {

  uint32_t startTime = millis();
  float integratedCurrent = 0;

  // Vamos sumando la medicion de corriente durante el tiempo
  // fijado integrationTimeMs (en milisegundos)
  while(millis()< startTime + integrationTimeMs) {
    uint16_t adcValue = analogRead(34);
    integratedCurrent = integratedCurrent + ((float)adcValue/4095.0 * 3.3)/Rshunt;
  }
  
  return integratedCurrent;
  
}

void setup() {
  
  Serial.begin(115200);
  
  ESP32PWM::allocateTimer(0);
  ESP32PWM::allocateTimer(1);
  ESP32PWM::allocateTimer(2);
  ESP32PWM::allocateTimer(3);
  servoMotor.setPeriodHertz(50);
  
  servoMotor.attach(33);

  pinMode(34, INPUT);

  Serial.println("Servo position: -45췈");
  servoMotor.write(45);
  delay(2000);

  torque = getCurrent(20);
  Serial.print("Initial current:\t");
  Serial.println(torque, DEC);

  Serial.println("Servo position: 45췈");
  servoMotor.write(135);

}

void loop() {

  torque = getCurrent(20);
  Serial.print("Current:\t");
  Serial.println(torque, DEC);
  
}
```

Ahora, para el caso sin obst치culo tenemos:

![image-20211101140230310](./assets/imgs/torque-trans-no-obstacle-integrated.png)

Vemos el mismo transitorio, pero destacan un par de aspectos debidos a la integraci칩n:

1. La se침al sufre un desfase.
2. La se침al dispone de menos muestras.
3. Los *bias* o errores estacionarios que puedan haber se acumulan en la medici칩n.
4. Realmente, dejamos de leer corriente (ni de broma esta pasando 50 A por el motor).

**El 칰ltimo punto es importante** y si no lo he modificado en el nombre de la funci칩n o en las gr치ficas es porque quiero que busqu칠is qu칠 magnitud estamos leyendo realmente al integrar la corriente y que, en el proyecto final que present칠is, indiqu칠is correctamente esa magnitud en el nombre de la funci칩n (en lugar de la actual `getCurrent()`) e indiqu칠is correctamente las unidades de la magnitud le칤da (que no ser치n amperios).

En el caso del obstaculo obtenemos:

![image-20211101140725189](./assets/imgs/torque-trans-obstacle-integrated.png)

En este caso, fijaros que tenemos los mismos pulsos porque el motor hace lo mismo que antes: si te pasas de fuerza el motor para de intentarlo y lo intenta unos milisegundos m치s tarde. Por esos siguen apareciendo los pulsos. Pero fijaros en su magnitud. Debido a que ahora estamos integrando, podemos observar que se obtiene un valor de par que dobla al caso sin obst치culo. Ahora, midiendo directamente el valor de la integral, s칤 que podemos diferenciar el caso en el que hay un obst치culo y en el que no.

#### *Challenge* #3

춰칔ltimo *challenge*! Incorpora la medici칩n del torque al *slave*, env칤aselo al *master* y haze que este 칰ltimo se lo env칤e al RoboDK junto con los 치ngulos Euler.

## Conclusiones

En esta sesi칩n hemos visto c칩mo o realizar una comunicaci칩n inal치mbrica entre dos m칩dulos basado en ESP32. En esa comunicaci칩n inal치mbrica, hemos enviado de un *master* a un *slave* los 치ngulos Euler de la orientaci칩n obtenida mediante una IMU. Con estos 치ngulos, con el *pitch* en particular, controlamos un servo con el *slave*. A su vez, el *slave* mide el par aplicado por el motor y se lo devuelve al *master*. Por 칰ltimo el *master* le env칤a toda la informaci칩n al RoboDK para simular la escena.
